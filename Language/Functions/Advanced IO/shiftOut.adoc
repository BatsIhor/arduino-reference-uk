---
title: shiftOut()
categories: [ "Functions" ]
subCategories: [ "Advanced I/O" ]
---

= shiftOut()


// OVERVIEW SECTION STARTS
[#overview]
--

[float]
=== Опис

Виводить байт даних виконуючи побітовий зсув по одному біту. Починає з самого старшого (тобто крайнього лівого) або найменшого (правого) біта. Кожен біт записується по черзі на контакт даних, після чого подається імпульс на тактовий контакт (подається `HIGH`, потім `LOW`), щоб вказати, що біт доступний.

Для взаємодії з пристроєм який тактується по передньому фронту сигналів переконайтесь, що тактовий контакт встановлений в `LOW` перед першим викликом `shiftOut()`, наприклад викличте `digitalWrite(clockPin, LOW)`.

Ця функція є програмною реалізацією SPI; також існує link:https://www.arduino.cc/en/Reference/SPI[бібліотека SPI], що використовує апаратну реалізацію, яка є швидшою, але працює лише на певних контактах.
[%hardbreaks]


[float]
=== Синтаксис
`shiftOut(dataPin, clockPin, bitOrder, value)`


[float]
=== Параметри
`dataPin`: контакт на який буде записуватись кожен біт. Очікує тип даних: `int`. +
`clockPin`: контакт тактового сигналу, який буде перемикатись під час передачі значення на *dataPin*. +
`bitOrder`: задає порядок в якому будуть зсуватись біти при виводі; може бути *MSBFIRST*, або *LSBFIRST*. (Most Significant Bit First Старший значний біт першим, або Least Significant Bit First Менший значний біт першим). +
`value`: дані для виводу. Очікує тип даних: `byte`.

[float]
=== Повертає
Нічого

--
// OVERVIEW SECTION ENDS




// HOW TO USE SECTION STARTS
[#howtouse]
--

[float]
=== Приклад коду
// Describe what the example code is all about and add relevant code   ►►►►► THIS SECTION IS MANDATORY ◄◄◄◄◄
Відповідна схема описана в https://arduino.cc/en/Tutorial/ShiftOut[посібнику з управління регістром зсуву 74HC595].

[source,arduino]
----
//**************************************************************//
//  Name    : shiftOutCode, Hello World                         //
//  Author  : Carlyn Maw,Tom Igoe                               //
//  Date    : 25 Oct, 2006                                      //
//  Version : 1.0                                               //
//  Notes   : Програма використовує регістр зсуву 74HC595       //
//          : для рахунку від 0 до 255                          //
//****************************************************************

// Контакт з'єднаний з ST_CP на 74HC595
int latchPin = 8;
// Контакт з'єднаний з SH_CP на 74HC595
int clockPin = 12;
// Контакт з'єднаний з DS на 74HC595
int dataPin = 11;

void setup() {
  // Контакти встановлюємо на вивід
  pinMode(latchPin, OUTPUT);
  pinMode(clockPin, OUTPUT);
  pinMode(dataPin, OUTPUT);
}

void loop() {
  // Процедура послідовного підрахунку
  for (int j = 0; j < 256; j++) {
    // Заземлюємо latchPin та утримуємо в LOW поки йде передача
    digitalWrite(latchPin, LOW);
    shiftOut(dataPin, clockPin, LSBFIRST, j);
    // Повертаємо контакт в HIGH, щоб сигналізувати мікросхемі,
    // що їй більше не потрібно слухати інформацію
    digitalWrite(latchPin, HIGH);
    delay(1000);
  }
}
----
[%hardbreaks]

[float]
=== Примітки та попередження
`dataPin` і `clockPin` вже повинні бути налаштовані як виходи за допомогою виклику link:../../digital-io/pinmode[pinMode()].

На даний момент shiftOut записує на вихід 1 байт (8 біт), тому для виведення значень більших за 255 потрібна двоступенева операція.
[source,arduino]
----
// Робимо для серійної передачі MSBFIRST
int data = 500;
// Зсуваємо та виводимо старший байт
shiftOut(dataPin, clock, MSBFIRST, (data >> 8));
// Виводимо молодший байт
shiftOut(dataPin, clock, MSBFIRST, data);

// Або для серійної передачі LSBFIRST
data = 500;
// Виводимо молодший байт
shiftOut(dataPin, clock, LSBFIRST, data);
// Зсуваємо та виводимо старший байт
shiftOut(dataPin, clock, LSBFIRST, (data >> 8));
----
[%hardbreaks]

--
// HOW TO USE SECTION ENDS


// SEE ALSO SECTION
[#see_also]
--

[float]
=== Дивись також

--
// SEE ALSO SECTION ENDS
